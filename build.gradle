plugins {
	id 'java'
	id 'application'
	id 'com.diffplug.spotless' version '8.1.0'
	id 'checkstyle'
}

group = 'epox'
version = '2.0.0'
description = 'WebAOM - Web Anime-o-Matic (AniDB file identification and organization)'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

application {
	mainClass = 'epox.webaom.WebAOM'
}

repositories {
	mavenCentral()
}

dependencies {
	runtimeOnly 'org.postgresql:postgresql:42.7.8'
	runtimeOnly 'com.mysql:mysql-connector-j:9.5.0'
	runtimeOnly 'com.h2database:h2:2.4.240'
}

tasks.register('generateVersionProperties') {
	doLast {
		def propsFile = file("${layout.buildDirectory.get()}/resources/main/version.properties")
		propsFile.parentFile.mkdirs()
		def props = new Properties()
		props['version'] = project.version.toString()
		props['buildDate'] = new Date().format('yyyy-MM-dd')
		propsFile.withWriter { props.store(it, null) }
	}
}

processResources.dependsOn generateVersionProperties

jar {
	manifest {
		attributes(
				'Main-Class': 'epox.webaom.WebAOM',
				'Implementation-Title': 'WebAOM',
				'Implementation-Version': version,
				'Created-By': 'epoximator',
				'Specification-Title': 'WebAOM',
				'Specification-Version': version,
				'Specification-Vendor': 'epoximator'
				)
	}

	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.compilerArgs.addAll([
		'-Xlint:unchecked',
		'-Xlint:deprecation'
	])
}

spotless {
	java {
		target 'src/main/java/epox/**/*.java'
		removeUnusedImports()
		eclipse().configFile("${rootDir}/config/eclipse-formatter.xml")
		formatAnnotations()
		trimTrailingWhitespace()
		endWithNewline()
		toggleOffOn()
	}

	groovyGradle {
		target '*.gradle'
		greclipse()
	}
}

checkstyle {
	toolVersion = '12.1.2'
	configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
	ignoreFailures = true
	maxWarnings = 0
	maxErrors = 0
}

tasks.withType(Checkstyle).configureEach {
	reports {
		xml.required = true
		html.required = true
	}
}
