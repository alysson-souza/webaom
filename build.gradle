plugins {
	id 'java'
	id 'application'
	id 'com.diffplug.spotless' version '7.0.2'
	id 'checkstyle'
}

group = 'epox'
version = '2.1.0'
description = 'WebAOM - Web Anime-o-Matic (AniDB file identification and organization)'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

application {
	mainClass = 'epox.webaom.WebAOM'
}

repositories {
	mavenCentral()
}

dependencies {
	runtimeOnly 'org.postgresql:postgresql:42.7.7'
	runtimeOnly 'com.mysql:mysql-connector-j:9.1.0'
}

jar {
	manifest {
		attributes(
				'Main-Class': 'epox.webaom.WebAOM',
				'Implementation-Title': 'WebAOM',
				'Implementation-Version': version,
				'Created-By': 'epoximator',
				'Specification-Title': 'WebAOM',
				'Specification-Version': '2.0.0',
				'Specification-Vendor': 'epoximator'
				)
	}

	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	options.compilerArgs.addAll([
		'-Xlint:unchecked',
		'-Xlint:deprecation'
	])
}

spotless {
	java {
		target 'src/main/java/epox/**/*.java'
		removeUnusedImports()
		googleJavaFormat('1.25.2').aosp()
		trimTrailingWhitespace()
		endWithNewline()
	}

	groovyGradle {
		target '*.gradle'
		greclipse()
	}
}

checkstyle {
	toolVersion = '10.21.1'
	configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
	ignoreFailures = true
	maxWarnings = 0
	maxErrors = 0
}

tasks.withType(Checkstyle) {
	reports {
		xml.required = true
		html.required = true
	}
}
